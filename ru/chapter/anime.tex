\hyphenation{аудио-драмах}
\hyphenation{аудио-записях}

\chapter{Аниме: загадочный и поразительный мир японской анимации}
Статья посвящена исследованию объекта Викиданных «аниме». С помощью SPARQL-запросов, вычисляемых на объектах типа «аниме»
в Викиданных, решены такие задачи: выведен упорядоченный список сэйю по числу озвученных ими аниме, построена гистограмма по числу сэйю, озвучивших одно и более аниме, построен граф, связывающий сэйю и озвученные ими аниме. 

\marginnote[0.0cm]{Сэйю — это японские актёры озвучивания. Сэйю обычно озвучивают роли персонажей в аниме, видеоиграх, фильмах, а также на радио и телевидении, или выступают в роли рассказчика в радиопостановках и аудиодрамах. Кроме того, голоса сэйю используются в рекламе, голосовых объявлениях, аудиозаписях книг и учебных материалов, а также для переозвучивания. Сэйю могут быть как мужчины, так и женщины.}

\begin{marginfigure}[0.0cm]
{
	\setlength{\fboxsep}{0pt}%
	\setlength{\fboxrule}{1pt}%
	\fcolorbox{gray}{gray}{\includegraphics{chapter/anime/seyu.jpg}}
}
\caption
[Сэйю]
{
Сэйю Кэндзи Акабанэ озвучивал роль персонажа Sasuke Sarutobi в видеоигре Ikemen Sengoku.\newline
2018 / numan / CC BY-SA 3.0
}
\label{fig:seyu}
\end{marginfigure}

\label{ch:anime}

\section{Экземпляры объекта «Аниме»}

Аниме — это японская анимация. У каждого аниме есть свои актёры озвучивания. В дальнейшем под «сэйю» будем понимать японского актёра озвучивания. В японской анимации слова «актёры озвучивания» и «сэйю» являются синонимами \cite{seiyu_def}. Под словом «тайтл» (от англ. «title», название) обычно понимают конкретное аниме \cite{anime_social}. В общем же смысле слово «тайтл» означает понятие, объединяющее различные виды продукции (от кинофильма до романа), созданные на основе конкретного произведения, за которым закреплено строго определённое название \cite{anime_title_def}.

\begin{itemize}
	\item Свойство: экземпляр (\href{https://www.wikidata.org/wiki/Property:P31}{P31})
	\item Объект: аниме (\href{https://www.wikidata.org/wiki/Q1107}{Q1107})
\end{itemize}

Если получить только список аниме, без учёта подклассов, с помощью такого запроса:

\begin{lstlisting}[ language=SPARQL, 
                    caption={\href{https://w.wiki/4ABw}{Список аниме без учёта подклассов.}\protect\footnotemark},
                    label=lst:anime,
                    texcl 
                    ]
# List of instances of anime
SELECT ?anime ?animeLabel
WHERE
{
    ?anime wdt:P31 wd:Q1107. # instance of anime
    SERVICE wikibase:label{bd:serviceParam
					     wikibase:language "ru,en,ja"}
}
\end{lstlisting}%
\footnotetext{Получено \num{216} результатов в 2021 году. Ссылка на SPARQL-запрос: \href{https://w.wiki/4ABw}{https://w.wiki/4ABw}}

В действительности в Викиданных объектов аниме гораздо больше, но они являются экземплярами не объекта «аниме», а его подклассов. Чтобы получить список жанров аниме и количество аниме, относящихся к этим жанрам, можно выполнить такой запрос:

\begin{lstlisting}[ language=SPARQL, 
                    caption={\href{https://w.wiki/4ABj}{Список жанров аниме и количество аниме, относящихся к этим жанрам.}\protect\footnotemark},
                    label=lst:anime_w_subclass,
                    texcl 
                    ]
# Select anime and its subclasses with number of titles
# corresponding to these subclasses
SELECT ?subAnime ?subAnimeLabel (COUNT(?subAnimeInst) AS ?count)
WHERE {
  ?subAnime wdt:P279* wd:Q1107.       # select anime subclass list
  ?subAnimeInst wdt:P31 ?subAnime # link titles and subclasses
    SERVICE wikibase:label{bd:serviceParam
					     wikibase:language "ru,en,ja"}
}
GROUP BY ?subAnime ?subAnimeLabel
ORDER BY DESC(?count)
\end{lstlisting}%
\footnotetext{Получено \num{11} результатов в 2021 году. Ссылка на SPARQL-запрос: \href{https://w.wiki/4ABj}{https://w.wiki/4ABj}}

Эта классификация не идеальна, так как есть большое смещение в сторону аниме-сериалов: из 4757 тайтлов 2917 отнесены к жанру «аниме-сериал» (61,3 \%); вероятно, классификация жанров аниме в Викиданных требует дальнейшего уточнения. Также в подклассы попали понятия, относящиеся не к общей классификации, а к отдельным аниме, например, \href{https://clck.ru/9cFfS}{«Евангелион»}.

Получим список всех аниме, включая тайтлы, относящиеся к жанрам аниме:

\begin{lstlisting}[ language=SPARQL, 
                    caption={\href{https://w.wiki/49zY}{Список всех аниме на Викиданных.}\protect\footnotemark},
                    label=lst:all_anime_list,
                    texcl 
                    ]
# List of instances of anime and subclasses of anime
SELECT ?anime ?animeLabel
WHERE
{
    ?anime wdt:P31/wdt:P279* wd:Q1107. # instance of anime/subclass
    SERVICE wikibase:label{bd:serviceParam
					     wikibase:language "ru,en,ja"}
}
\end{lstlisting}%
\footnotetext{Получено \num{683} результата в 2018 году и \num{4757} результатов в 2021 году. Ссылка на SPARQL-запрос: \href{https://w.wiki/49zY}{https://w.wiki/49zY}}

Аниме, о которых есть наиболее полная информация на Викиданных: \href{https://www.wikidata.org/wiki/Q4277}{Гуррен-Лаганн}, \href{https://www.wikidata.org/wiki/Q4292}{Space Battleship Yamato}, \href{https://www.wikidata.org/wiki/Q4316}{Project A-ko}

Аниме с малоинформативными записями на Викиданных: \href{https://www.wikidata.org/wiki/Q18692527}{Charlotte}, \href{https://www.wikidata.org/wiki/Q20043638}{Dagashi Kashi}, \href{https://www.wikidata.org/wiki/Q19750843}{KonoSuba}

Среди всех аниме-тайтлов в Викиданных больше всего свойств по данным сервиса ProWD у \href{https://www.wikidata.org/wiki/Q1004318}{Fullmetal Alchemist: The Sacred Star of Milos} (23 свойства).

\subsection{Упорядоченный список сэйю по числу озвученных ими аниме}

Практически в любом аниме присутствуют несколько сэйю. Большинство сэйю озвучили за свою карьеру несколько тайтлов, а многие даже несколько десятков тайтлов. Талантливых сэйю приглашают озвучивать сразу несколько персонажей в одном аниме. Одним из самых популярных сэйю является \href{https://clck.ru/YSCoP}{Хироси Камия}, который за свою карьеру принял участие в озвучке более 180 аниме. Самым известным аниме с его участием является \href{https://clck.ru/YSCrG}{«Атака титанов»}, где он озвучил одного из главных персонажей — капитана Леви.

Построим упорядоченный список сэйю по числу озвученных ими аниме. 

\begin{lstlisting}[ language=SPARQL, 
                    caption={\href{https://w.wiki/49zz}{Упорядоченный список сэйю по числу озвученных ими аниме.}\protect\footnotemark},
                    label=lst:seiyu_titles_sorted,
                    texcl 
                    ]
# Ordered list of seiyu according to the number of anime
# where they took part in
SELECT ?seiyu (SAMPLE(?label) AS ?seiyuLabel) (COUNT(?anime) AS ?count)
WHERE
{
  ?anime wdt:P31/wdt:P279* wd:Q1107;	 # instance of anime/subclass
         wdt:P725 ?seiyu. 	             # instance of seiyu
  ?seiyu rdfs:label ?label	             # subclass of label
    SERVICE wikibase:label{bd:serviceParam
					     wikibase:language "ru,en,ja"}
}
GROUP BY ?seiyu		    # group by seiyu 
ORDER BY DESC(?count)	# order by count of voiced anime
\end{lstlisting}%
\footnotetext{Получено \num{148} результатов в 2018 году и \num{2684} результата в 2021 году. Ссылка на SPARQL-запрос: \href{https://w.wiki/49zz}{https://w.wiki/49zz}}

\subsection{Гистограмма по числу сэйю, озвучивших одно и более аниме}

Было бы интересно построить гистограмму (линейную диаграмму) из сэйю, озвучивших аниме (чем больше аниме озвучил сэйю, тем дальше на диаграмме он будет находиться, «правее» в данном случае).

\begin{lstlisting}[ language=SPARQL, 
                    caption={\href{https://w.wiki/4HFv}{Построение гистограммы по числу сэйю, озвучивших одно и более аниме}\protect\footnotemark},
                    label=lst:seiyu_titles_hist,
                    texcl 
                    ]
# Histogram of the number of seiyu who acted in one or more anime
#defaultView:LineChart
SELECT ?haveseiyu (COUNT(?haveseiyu) AS ?quantity) WHERE {
  {
     SELECT (COUNT(?seiyu) AS ?haveseiyu) WHERE {
       ?anime wdt:P31/wdt:P279* wd:Q1107;
              wdt:P725 ?seiyu.
    SERVICE wikibase:label{bd:serviceParam
					     wikibase:language "ru,en,ja"}
     }
     GROUP BY ?anime             # group list by number of voiced anime
     ORDER BY DESC(?haveseiyu) 
  }
}
GROUP BY ?haveseiyu              # group anime by seiyu quantity
ORDER BY DESC(?haveseiyu)        # order by seiyu quantity (descending)
\end{lstlisting}%
\footnotetext{Получено \num{13} результатов в 2018 году и \num{58} результатов в 2021 году. Ссылка на SPARQL-запрос: \href{https://w.wiki/4HFv}{https://w.wiki/4HFv}}

Очевидно, что чем большее количество аниме берётся в расчёт, тем меньшее количество сэйю участвует в озвучке (рис. 1). Многие сэйю, как показано на диаграмме, озвучили только одно аниме. Это может быть связано с неполнотой Викиданных.

\begin{figure*}[h]

    \setlength{\fboxsep}{0pt}%
    \setlength{\fboxrule}{1pt}%
    \fcolorbox{gray}{gray}{\includegraphics[width=\linewidth]{./chapter/anime/Seiyu_histogram_2021_ru.jpg}}
	\caption[Гистограмма, которая показывает число аниме, озвученных различными сэйю, 2021.]{Гистограмма, которая показывает число аниме, озвученных различными сэйю, 2021. Гистограмма построена на основе данных, полученных с помощью запроса~\protect\ref{lst:seiyu_titles_hist}.}%
    \label{fig:Seiyu_histogram_2021_ru}%
\end{figure*} 

\subsection{Граф, связывающий сэйю и озвученные ими аниме}

Как было сказано ранее, некоторые сэйю могут озвучивать сразу нескольких персонажей в одном аниме или озвучивать несколько аниме. Построим граф, связывающий сэйю и озвученные ими аниме, чтобы нагляднее показать эту взаимосвязь. 

\begin{lstlisting}[ language=SPARQL, 
                    caption={\href{https://w.wiki/4HFt}{Построение графа, связывающего сэйю и озвученные ими аниме}\protect\footnotemark},
                    label=lst:seiyu_graph,
                    texcl 
                    ]
# Graph of seiyu with more than one anime
#defaultView:Graph
SELECT DISTINCT ?item ?itemLabel ?rgb ?link
WHERE
{ # voice actors (seiyu) with more than one anime
  VALUES ?toggle { true false }
  VALUES ?seiyu { wd:Q6381410 wd:Q1347031 wd:Q1207010 
           wd:Q233902  wd:Q1323728 wd:Q2440809 
           wd:Q355173  wd:Q957795  wd:Q50033}
  ?anime  wdt:P31/wdt:P279* wd:Q1107; # instance of anime/subclass
          wdt:P725 ?seiyu;            # seiyu who voiced this anime 
    SERVICE wikibase:label{bd:serviceParam
					     wikibase:language "ru,en,ja"}
  BIND(IF(?toggle,?anime,?seiyu) AS ?item).
  BIND(IF(?toggle,?animeLabel,?seiyuLabel) AS ?itemLabel).
  BIND(IF(?toggle,"FFFFFF","7FFF00") AS ?rgb).
  BIND(IF(?toggle,"",?anime) AS ?link).
}
\end{lstlisting}%
\footnotetext{Получено \num{826} результатов в 2018 году и \num{494} результата в 2021 году. Ссылка на SPARQL-запрос: \href{https://w.wiki/4HFt}{https://w.wiki/4HFt}}

\begin{figure*}[h]
\centering
	\includegraphics[width=0.95\textwidth]{./chapter/anime/Seiyu_graph_2021_ru.jpg}
	\caption[Фрагмент графа, связывающего сэйю и озвученные ими аниме, 2021.]{Фрагмент графа, связывающего сэйю и озвученные ими аниме, 2021. Граф построен на основе данных, полученных с помощью запроса~\protect\ref{lst:seiyu_graph}.}%
      \label{fig:Seiyu_graph_2021_ru}%
\end{figure*} 
